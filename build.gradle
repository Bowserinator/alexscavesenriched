plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id 'java'
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_name
}
def generatedResources = file("src/generated")
sourceSets {
    main {
        resources.srcDir generatedResources
    }
}
loom {
    silentMojangMappingsLicense()

    forge {
        mixinConfig 'alexscavesenriched.mixins.json'
    }
    runs {
        data {
            data()
            programArgs "--all", "--mod", "alexscavesenriched"
            programArgs "--output", generatedResources.absolutePath
        }
        client {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            vmArgs "-XX:+AllowEnhancedClassRedefinition"
        }
    }
}
repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url = 'https://maven.parchmentmc.org' }

}

dependencies {
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixin_extras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${project.mixin_extras_version}"))
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:2023.09.03@zip")

    }
    // mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
    forge "net.minecraftforge:forge:$project.forge_version"
    modImplementation "maven.modrinth:alexs-caves:2.0.2"
    modImplementation "maven.modrinth:patchouli:1.20.1-84.1-forge"
    modImplementation "maven.modrinth:emi:1.1.22+1.20.1+forge"
    modImplementation "maven.modrinth:citadel:2.6.2"
    modApi "me.shedaniel.cloth:cloth-config-forge:11.1.136"
    modCompileOnlyApi("dev.emi:emi-forge:1.1.22+1.20.1:api")
    modRuntimeOnly("dev.emi:emi-forge:1.1.22+1.20.1")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
